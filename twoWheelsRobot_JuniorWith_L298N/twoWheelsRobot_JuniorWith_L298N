#define CUSTOM_SETTINGS
#define INCLUDE_GAMEPAD_MODULE
#include <Dabble.h>
#include <Servo.h>  // Include the Servo library

// Left Motor
int in1 = 13;
int in2 = 12;

// Right Motor
int in3 = 11;
int in4 = 10;

// Servo
Servo myServo;
int servoPin = 9;        // Connect your servo signal pin here
int angle = 90;          // Start at middle position (0 to 180)

void setup() {
  Serial.begin(9600);
  Dabble.begin(9600, 3); // RX = 3
  pinMode(in1, OUTPUT);
  pinMode(in2, OUTPUT);
  pinMode(in3, OUTPUT);
  pinMode(in4, OUTPUT);

  myServo.attach(servoPin);
  myServo.write(angle);  // Initialize at middle position

  Serial.println("Remote Control Ready!");
}

void loop() {
  Dabble.processInput();
  handleButtonPress();
  delay(50);
}

// Movement Functions
void moveForward() {
  digitalWrite(in1, HIGH); digitalWrite(in2, LOW);
  digitalWrite(in3, HIGH); digitalWrite(in4, LOW);
}

void moveBackward() {
  digitalWrite(in1, LOW); digitalWrite(in2, HIGH);
  digitalWrite(in3, LOW); digitalWrite(in4, HIGH);
}

void turnLeft() {
  digitalWrite(in1, HIGH); digitalWrite(in2, LOW);
  digitalWrite(in3, LOW);  digitalWrite(in4, HIGH);
}

void turnRight() {
  digitalWrite(in1, LOW);  digitalWrite(in2, HIGH);
  digitalWrite(in3, HIGH); digitalWrite(in4, LOW);
}

void stop() {
  digitalWrite(in1, LOW); digitalWrite(in2, LOW);
  digitalWrite(in3, LOW); digitalWrite(in4, LOW);
}

// Handle gamepad button input
void handleButtonPress() {
  if (GamePad.isUpPressed()) {
    Serial.println("Forward");
    moveForward();
  }
  else if (GamePad.isDownPressed()) {
    Serial.println("Backward");
    moveBackward();
  }
  else if (GamePad.isLeftPressed()) {
    Serial.println("Turn Left");
    turnLeft();
  }
  else if (GamePad.isRightPressed()) {
    Serial.println("Turn Right");
    turnRight();
  }
  else if (GamePad.isSquarePressed()) {
    Serial.println("Spin Left");
    turnLeft();
  }
  else if (GamePad.isCirclePressed()) {
    Serial.println("Spin Right");
    turnRight();
  }
  else if (GamePad.isTrianglePressed()) {
    angle = constrain(angle + 5, 0, 180);  // Increase angle
    myServo.write(angle);
    Serial.print("Servo Angle Increased: ");
    Serial.println(angle);
  }
  else if (GamePad.isCrossPressed()) {
    angle = constrain(angle - 5, 0, 180);  // Decrease angle
    myServo.write(angle);
    Serial.print("Servo Angle Decreased: ");
    Serial.println(angle);
  }
  else {
    stop();
  }
}
